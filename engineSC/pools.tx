Pls:
    globals*=Global
    rules+=Rule
;

Global:
    'global'
    variable=Variable
;

Rule:
    ('salience' salience=INT)?
    loop=Loop
    'rule'
    '"'
    name=RuleName
    '"'
    'when'
    lhs=LHS
    'then'
    rhs=RHS
    'end'
;

Loop:
    'no-loop' | 'lock-on-active' | ''
;

RuleName:
   /[\w\-_& ]+/
;

LHS:
  conditions+=Condition
;

Condition:
    (variable=Variable ':')?
    not=UnaOperator?
    factClass=FactClass
    '(' evaluations=Evaluations? ')'
;

Variable:
    /\$[A-Za-z][A-Za-z0-9_]*/
;

FactClass:
    /[A-Za-z][A-Za-z0-9_]*/
;

Evaluations:
   firstEvaluationChoice=EvaluationChoice
   subsequentEC*=SubsequentEC
;

SubsequentEC:
    logOperator=LogOperator
    evaluationChoice=EvaluationChoice
;

EvaluationChoice:
    EvaluationGrouped | Evaluation
;

EvaluationGrouped:
    '('
        evaluations=Evaluations
    ')'
;

Evaluation:
    operand=RelOperand
    continuation=Continuations
;

Continuations:
    firstContinuationChoice=ContinuationChoice
    subsequentCC*=SubsequentCC
;

SubsequentCC:
    logOperator=LogOperator
    continuationChoice=ContinuationChoice
;

ContinuationChoice:
    ContinuationGrouped | Continuation
;

ContinuationGrouped:
    '('
        continuations=Continuations
    ')'
;

Continuation:
    relOperator=RelOperator
    operand=RelOperand
;

LogOperator:
   ',' | 'and' | 'or'
;

RelOperator:
    '<=' | '>=' | '>' | '<' | '==' | '!=' | 'contains'
;

UnaOperator:
    /[a-z]*/
;

Field:
    field=/[A-Za-z][A-Za-z0-9_]*/
;

RelOperand:
    Field  | NUMBER | BOOL | STRING
;


RHS:
  /(.*\n)*?(?=end)/
;
