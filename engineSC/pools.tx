Pls:
    globals*=Global
    rules+=Rule
;

Global:
    'global'
    variable=Variable
;

Rule:
    ('salience' salience=INT)?
    loop=Loop
    'rule'
    '"'
    name=RuleName
    '"'
    'when'
    lhs=LHS
    'then'
    rhs=RHS
    'end'
;

Loop:
    'no-loop' | 'lock-on-active' | ''
;

RuleName:
   /[\w\-_& ]+/
;

LHS:
  conditions+=Condition
;

Condition:
    (variable=Variable ':')?
    factClass=FactClass
    '(' evaluations=Evaluations? ')'
;

Variable:
    /\$[A-Za-z][A-Za-z0-9_]*/
;

FactClass:
    /[A-Za-z][A-Za-z0-9_]*/
;

Evaluations:
   firstEval=Evaluation
   couples*=Couple
;

Couple:
    logOperator=LogOperator
    evaluation=Evaluation
;

LogOperator:
   ',' | 'and' | 'or'
;

Evaluation:
    operand1=RelOperand
    relOperator=RelOperator
    operand2=RelOperand
;

RelOperator:
    '<=' | '>=' | '>' | '<' | '==' | '!=' | 'contains'
;

Field:
    field=/[A-Za-z][A-Za-z0-9_]*/
;

RelOperand:
    Field  | NUMBER | BOOL | STRING
;

RHS:
  /(.*\n)*?(?=end)/
;




